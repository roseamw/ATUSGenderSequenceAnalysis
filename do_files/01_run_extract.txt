#delimit;
clear;
set more off;

/// Set file path to local repository 
/// cd "C:\Users\Joanna\Dropbox\Repositories\ATUSGenderSequenceAnalysis"

local logdate = string( d(`c(current_date)'), "%dCY.N.D" );

log using log_files\01_run_extract_`logdate'.log, t replace;

local run_setting quietly;
local exnum 00520;

`run_setting' {;
cd .\data\atus_extracts;

/*1. check to see if the unzipped data file exists*/
cap confirm file atus_`exnum'.dat;

/*1a. if the file exists, convert to stata format*/
if !_rc {;
	noisily disp "Running ATUS-X extract...";
	qui do atus_`exnum'.do;
	numlabel, add;

	/*2. Make sure the directory where we want to save the data file exists*/
	cap confirm file ..\generated_data_files\nul;

	/*2a. If it doesn't,  then create it.*/
	if _rc {;
		cd ..;
		shell mkdir generated_data_files;
		cd generated_data_files;
		};
	/*2b. If it does exist, just change directories so we can save data file in the correct place.*/
	else {;
		cd ..\generated_data_files;
		};
		
	save extract.dta, replace;
	noisily disp "File successfully created!";
	
	/*2c. Now get hierarchical file in a usable format*/
	keep if rectype==1;
	
	qui foreach v of var * {;
		count if missing(`v');
		if r(N) == _N drop `v';
		};
	
	save extract_h, replace;
	
	clear;
	use extract.dta;
	keep if rectype==2;
	
	qui foreach v of var * {;
		count if missing(`v');
		if r(N) == _N drop `v';
		};
	
	save extract_p, replace;
	
	clear;
	use extract.dta;
	keep if rectype==3;
	
	qui foreach v of var * {;
		count if missing(`v');
		if r(N) == _N drop `v';
		};
	
	save extract_a, replace;
	
	clear;
	use extract.dta;
	keep if rectype==4;
	
	qui foreach v of var * {;
		count if missing(`v');
		if r(N) == _N drop `v';
		};
	
	save extract_w, replace;
	
	clear;
	use extract_p;
	merge m:1 caseid using extract_h;
	save extract_hp, replace;
	
	};

/*1b. if the file does not exist, throw an error with recommended next steps*/
else {;
	noisily disp as error "!!!!!WARNING!!!!!: ATUS-X extract atus_`exnum'.dat is missing! Unzip .gz file and run this do file again.";
	};

cd ../../do_files;
};
log close;